
/* ===  RSdesign Analysis with covariates ( Day, Grade) === */
data Experiment;
input Day Grade Time Temp Pressure Yield @@;
datalines;
1 67 -1 -1 -1 32.98 1 68 -1 1 1 47.04 1 70 1 -1 1 67.11 1 66 1 1 -1 26.94 1 74 0 0 0 103.22 1 68 0 0 0 42.94
2 75 -1 -1 1 122.93 2 69 -1 1 -1 62.97 2 70 1 -1 -1 72.96 2 71 1 1 1 94.93 2 72 0 0 0 93.11 2 74 0 0 0 112.97
3 69 1.633 0 0 78.88 3 67 -1.633 0 0 52.53 3 68 0 1.633 0 68.96 3 71 0 -1.633 0 92.56 3 70 0 0 1.633 88.99 
3 72 0 0 -1.633 102.50 3 70 0 0 0 82.84 3 72 0 0 0 103.12
;

/*  no factors are significant without covariates */
proc rsreg data=Experiment;
model Yield = Time Temp Pressure ;
run;

/*  every factor is significant with covariates */
data experiment; set experiment;
d1=(day=1);
d2=(day=2);
run;

proc rsreg data=Experiment;
model Yield = d1 d2  grade Time Temp Pressure / covar=3 ; 
run;

/* 3d plots */
ods graphics on;
proc rsreg data=Experiment  plots(unpack)=surface(3d);
model  Yield = d1 d2  grade Time Temp Pressure / covar=3 ; 
run;
ods graphics off;

/*  3d plot with given covariate values */
ods graphics on;
proc rsreg data=Experiment
 plots(unpack)=surface(3d at(Pressure=-0.487 d1=0 d2=1  grade=70));
model Yield = d1 d2  grade Time Temp Pressure / covar=3 ; 
ods select 'Time * Temp = Pred';
run;
ods graphics off;

